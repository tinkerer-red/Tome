# Formatting Scripts


## Setting up Your Scripts
Getting started, each script you add using `Tome.site.add` must contain the Tome tags `@category` and `@title`. 

This will set the name of the page in the sidebar of your site as `title` and the category it appears under would be `category`.

![Imgur](https://i.imgur.com/DfzC3kE.png)

As shown in the example above the script would have contained `@title Unit Array` and `@category Units`

## Basic Formatting of JSDoc
Tome works by parsing JSDoc tags from within your scripts. It is important that your JSDoc is structured in a specific, feather compliant way. Most of the JSDoc tags are basic, commonly supported ones, and others are unique to Tome.

For an understanding of how JSDoc comments work in GameMaker, check out [this](https://manual.gamemaker.io/monthly/en/#t=The_Asset_Editors%2FCode_Editor_Properties%2FJSDoc_Script_Comments.htm) page of the manual.

?> Also it's worth noting that JSDoc comments like `@param` or `@returns` which require a datatype like `@param {string} foo` don't necessarily need to conform to feather's rules, and can be literally anything.

As a basic example, here is a function from this library (yes, Tome's documentation was generated with Tome!):

```gml
#region /// @func Tome.site.setHomepage(file, [raw])
Tome.site.setHomepage = function(_file, _raw = false){
    
    __tomeUpdateDebug();
    
    if (!file_exists(_file)){
        _file = $"{Tome.__data.projectDirectory}notes/{_file}/{_file}.txt";
    }
    
    if (!file_exists(_file)){
        __tomePushWarning("The given file doesn't seem to exist.");
        exit;
    }

    if (_raw){
        Tome.site.addRaw(_file, "homepage", "homepage");
    }else{
{any}        var _markdownData = __tomeParseDocumentationFile(_file, true);
    
        if (!_markdownData.success){
            __tomePushWarning("Something went wrong during parsing. Please check warnings above for more information.", true);
        }else{ 
            array_push(Tome.__data.docsPageItems, _markdownData);
        }
    }
};
#endregion // Tome.site.setHomepage
```

### Things To Note Here
- The order of the JSDoc comments must always be the following:
	1. `@func` or its alternatives
	2. `@desc` or its alternative
	3. `@param` or its alternatives
	4. `@returns` or its alternative
<br>
* Additionally you can:
    - Use `@deprecated` to visually indicate a constructor, function, or method is no longer recommended for use.
    - Use `@slug`/`@insert` after parameters to include a footer for the function
<br>
When making arguments optional you can indicate this to Tome by wrapping the name in square brackets like so `[raw]` from within the signature tag. Any arguments indicated this way will be marked as optional for the parameters markdown table.
If you choose to leave the signature tag without indicators you can also indicate arguments as optional in the appropriate @param (or alternative) tag. This is also achieved by simply wraping the name in square brackets.

### Constructors
- Tome will automattically format constructors and their methods as well! Just put `@constructor` before defining the constructor function and then use `@method` for each method within the constructor instead of the usual `@func`

#### Example
```gml
/// @constructor
/// @func dropTable(maxItemAmount)
/// @param {real} maxItemAmount The amount of items that the droptable contains, after all the items have been dropped, EMPTY_STRUCT will be returned
function dropTable(_maxItemAmount) constructor {
    __drops = [];
    __totalItemAmount = _maxItemAmount;
	
	/// @method getItemAmountLeft()
	/// @desc Returns the number of remaining items in the drop table
	/// @return {real}
	static getItemAmountLeft = function(){
		return __totalItemAmount;
	}
	
	/// @method getDrops()
	/// @desc Returns the __drop array
	/// @return {array<drops>}
	static getDrops = function(){
		var _copyOfDrops = [];
		var _i = 0;
		
		repeat(array_length(__drops)){
			var _currentDrop = __drops[_i];
			
			array_push(_copyOfDrops, {
				itemConstructorName: _currentDrop.__itemConstructorName,
				dropAmount: _currentDrop.__dropAmount,
				dropChance: _currentDrop.__dropChance	
			});
			_i++;	
		}
		return _copyOfDrops;	
	}
	
	/// @method dropAdd(itemConstructorName, dropChance, dropAmount)
	/// @param {string} itemConstructorName The name of the item 
	/// @param {real} dropChance The chance that the item will be dropped when rolling a drop ex: 128 = 1 in 128
	/// @param {real} dropAmount The item quantity that will be dropped if this item is rolled
	/// @return {undefined}
	static dropAdd = function(_itemConstructorName, _dropChance, _dropAmount) {
		var _drop = new __drop(_itemConstructorName, _dropChance, _dropAmount);
		array_push(__drops, _drop);
	}
	
	/// @method dropRoll()
	/// @desc Rolls a drop from the table. 
	/// @return {struct.drop|struct.EMPTY_STRUCT} A struct with properties: `itemConstructorName` and `dropAmount`. If no item is rolled, EMPTY_STRUCT will be returned.
	static dropRoll = function() {
	    if (__totalItemAmount <= 0) {
	        return EMPTY_STRUCT; // Return EMPTY_STRUCT if no more drops can be rolled
	    }

	    // Create a cumulative weight array
	    var _cumulativeWeights = [];
	    var _cumulativeWeight = 0;

	    for (var _i = 0; _i < array_length(__drops); _i++) {
	        _cumulativeWeight += 1 / __drops[_i].__dropChance;
	        _cumulativeWeights[_i] = _cumulativeWeight;
	    }

	    // Generate a random number between 0 and total cumulative weight
	    var _randomWeight = random(_cumulativeWeight);

	    // Roll for a single drop
	    for (var _i = 0; _i < array_length(__drops); _i++) {
	        var _weight = (_i == 0) ? _cumulativeWeights[_i] : _cumulativeWeights[_i] - _cumulativeWeights[_i - 1];
	        if (_randomWeight <= _weight) {
	            // Decrement the totalItemAmount
	            __totalItemAmount -= __drops[_i].__dropAmount;
				var _drop = __drops[_i];

	            return {itemConstructorName: _drop.__itemConstructorName, dropAmount: _drop.__dropAmount}; // Return the drop struct
	        }
	        _randomWeight -= _weight;
	    }

	    return EMPTY_STRUCT; // Return EMPTY_STRUCT if no item is rolled
	}
	
	static __drop = function(_itemConstructorName, _dropChance, _dropAmount) constructor {
	    __itemConstructorName = _itemConstructorName;
	    __dropAmount = _dropAmount;
		__dropChance = _dropChance;
	}
}
```

### Additional Tome Tags

#### `@text`
For adding a single/multi-line block of [markdown](https://www.markdownguide.org/getting-started/) text.

##### Examples:
`/// @text **This is some bold text**`

```gml
/// @text **Bold** and *italisized* text
/// across muitiple lines!
/// ## You can use keep going!
```

#### `@code`
For embeding a code block on your site like:

![Imgur](https://imgur.com/dx3tyfK.png)

##### Example
```gml
/// @code
/// toolInv = new inventory(4, ["itemTool"], false);
/// storage = new inventory(64, ["item"], true);
```

## Slugs

The use of slugs can help reduce bloat in your implementation file, if you chose to add examples as a part of your JSDoc. 

### Defining slugs
You can create note files that contain additonal documentation for functions that can then be insterted into your JSDocs by using the @slug or @insert tag.

!> Slugs must be defined in a note file.


You can create a note that holds multiple slugs such as


```
/// @slug some-slugExamples:

'''
foo();
foo(bar);
foo(bar, fib);
'''

/// @slug some-other-slugExamples:

'''
boo();
boo(far);
boo(far, bif);
'''
```




### Using slugs
To add a slug into the JSDoc markdown you simply use the @slug tag as such


```gml
/// @function someFunction(someParameter)
/// @desc A function that does something
/// @slug some-slug/// @param {any} someParam A parameter that changes the behavior of the function
/// @returns {undefined}
```

This will result in markdown that outputs like this

<p align = "center">
    <img src = "https://i.imgur.com/5ZQsHnf.png" /> <br>
</p>


## Full Example from Tome's internal testing docs:
```gml

/// @text ## Data Structures
/// 
/// The following structures are lightweight and contain no internal methods. They are strictly used for data passing.

/// @constructor
/// @func Coordinate3D(x, y, z)
/// @desc A simple constructor with no methods to test empty group rendering. Used to represent a point in 3D space.
/// @slug warning-callout
/// @param {real} x The X coordinate.
/// @param {real} y The Y coordinate.
/// @param {real} z The Z coordinate.
function Coordinate3D(_x, _y, _z) constructor {
    x = _x;
    y = _y;
    z = _z;
}


/// @text ### Utility Functions
/// 
/// These standard functions operate on the lightweight data structures.
/// We are testing standard functions interspersed between constructors.

/// @func calculate_distance_3d(point_a, point_b)
/// @desc A regular function sitting next to constructors. Calculates the Euclidean distance between two 3D coordinates.
/// @slug example-usage-box
/// @param {struct.Coordinate3D} point_a The first point.
/// @param {struct.Coordinate3D} point_b The second point.
/// @return {real} The distance between the points.
function calculate_distance_3d(_point_a, _point_b) {
    // Math logic here
    return 0; 
}

/// @func invert_coordinate(point)
/// @desc Flips the signs of all axes in a Coordinate3D struct. Testing another standalone function.
/// @param {struct.Coordinate3D} point The coordinate to invert.
/// @return {struct.Coordinate3D} A new inverted coordinate struct.
function invert_coordinate(_point) {
    return new Coordinate3D(-_point.x, -_point.y, -_point.z);
}

/// @text ## Complex Entities
/// 
/// Finally, a standard constructor with methods can exist in the same file without breaking the context of the previous standalone functions.

/// @constructor
/// @func PhysicsBody(coordinate)
/// @desc A complex entity that utilizes the lightweight data structures.
/// @param {struct.Coordinate3D} coordinate The starting position.
function PhysicsBody(_coordinate) constructor {
    position = _coordinate;
    velocity = new Coordinate3D(0, 0, 0);

    /// @method apply_force(force_vector)
    /// @desc Adds a force vector to the current velocity.
    /// @param {struct.Coordinate3D} force_vector The direction and magnitude of the force.
    /// @return {undefined}
    static apply_force = function(_force_vector) {}
    
    /// @method update_position()
    /// @desc Applies the current velocity to the position coordinate.
    /// @return {undefined}
    static update_position = function() {}
}

```
## All Tags

| Tag Name                                    | Purpose                                                                                 | Multi-line? |
| ------------------------------------------- | --------------------------------------------------------------------------------------- | ----------- |
| `@constructor`                              | To define a function as a constructor function                                          | No          |
| `@func`, `@function`                        | To display the name of the function and it's parameters                                 | No          |
| `@method`                                   | Used in place of `@func`/`@function` for methods within a constructor                   | No          |
| `@desc`, `@description`                     | To display a description of the function                                                | Yes         |
| `@param`, `@parameter`, `@arg`, `@argument` | To display a parameter's name, datatype and description                                 | No          |
| `@return`, `@returns`                       | To display the datatype or datatype + description of the value returned by the function | No          |
| `@deprecated`                               | To display that the constructor/function/method is deprecated.                          | No          |
| `@text`                                     | Displays a block of text, formatted using markdown                                      | Yes         |
| `@code`, `@example`                         | Displays a code block                                                                   | Yes         |
| `@title`                                    | The title of the page as displayed in the sidebar                                       | No          |
| `@category`                                 | What category in the sidebar that the page will be put under                            | No          |
| `@end`                                      | Signals Tome to close a constructor context. This is only needed if you plan to place unrelated text or code after you've closed out a constructor| No |
| `@pass`                                     | Signals Tome to change its passing state. See table below for more information          | No          |



When using `@pass` there are a few different options that change the bavior of Tome's parser.

--- 

| Tag Value         | Purpose           |
| ----------------- | ----------------- |
| `true`            | Signals Tome to begin ignoring JSDoc tags and text Useful for private functions you don't want publicly documented. |
| `tag`             | Signals Tome to begin ignoring JSDoc tags and adding line as raw text. Useful if you want to use Tome to document Tome. Not so much otherwise. |
| `false`           | Signals Tome to reset its passing state and process JSDoc tags and text. |
| Anything else     | Signals Tome to ignore anything this line may have on it and instead add it directly as text. |

